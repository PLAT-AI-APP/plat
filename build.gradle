plugins {
    id 'org.springframework.boot' version '4.0.2' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

subprojects {

    group = projectGroup
    version = applicationVersion

    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
    }

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(25)
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    tasks.register("prepareKotlinBuildScriptModel"){}
}

project(":plat-ai") {

    processResources.dependsOn("copySecret")

    tasks.register("copySecret", Copy) {
        from "${project.rootDir}/plat-config/ai"
        include "application*.yaml"
        into "${project.rootDir}/plat-ai/src/main/resources"
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'

        implementation 'org.springframework.ai:spring-ai-starter-model-openai'
        implementation 'org.springframework.ai:spring-ai-starter-model-anthropic'
        implementation 'org.springframework.ai:spring-ai-starter-model-google-genai'

        implementation project(':plat-data')

    }
}

project(":plat-data") {

    apply plugin: 'java-library'

    processResources.dependsOn("copySecret")

    tasks.register("copySecret", Copy) {
        from("${project.rootDir}/plat-config/data") {
            include "data*.yaml"
        }
        from("${project.rootDir}/plat-config/common") {
            include "*.yaml"
        }
        into "${project.rootDir}/plat-data/src/main/resources"
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        api 'org.springframework:spring-tx'
        testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'

        implementation 'com.h2database:h2'
        runtimeOnly 'com.mysql:mysql-connector-j'
        testImplementation 'com.h2database:h2'

        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
        testImplementation 'org.springframework.security:spring-security-test'

        // JWT
        implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
        runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
        runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'
    }
}

project(":plat-redis") {

    apply plugin: 'java-library'

    bootJar { enabled = false }
    jar { enabled = true }

    dependencies {
        api 'org.springframework.boot:spring-boot-starter-data-redis'
    }
}

project(":plat-boot") {

    processResources.dependsOn("copySecret")

    tasks.register("copySecret", Copy) {
        from "${project.rootDir}/plat-config/boot"
        include "application*.yaml"
        into "${project.rootDir}/plat-boot/src/main/resources"
    }

    dependencies {
        implementation project(':plat-data')
        implementation project(':plat-redis')
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-mail'
    }
}